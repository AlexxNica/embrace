apply plugin: 'java'
apply plugin: 'idea'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/google-snapshots"
    }
}

sourceSets {
    generated {
        java {
            srcDir 'build/generated-src/java/main/'
        }
    }
}

dependencies {
    generatedCompile project(":")
    generatedCompile "com.google.gwt:gwt-user:2.8.0-SNAPSHOT"
}

idea {
    module {
        excludeDirs = [file(".gradle")]
        ["classes", "docs", "dependency-cache", "distributions", "libs", "scripts", "reports", "resources", "test-results", "tmp"].each {
            excludeDirs << file("$buildDir/$it")
        }
    }
}


dependencies {
    compile project(':tools')
    compile "com.google.gwt:gwt-user:2.8.0-SNAPSHOT"
}

task idl2java(type: JavaExec) {
    outputs.files sourceSets.generated.output
    classpath = sourceSets.main.runtimeClasspath
    main = "com.giddyplanet.embrace.webapis.internal.Generator"
}

compileGeneratedJava.dependsOn idl2java

jar {
    from sourceSets.generated.output
    from sourceSets.generated.allSource
    exclude "**/webapis/internal"
}
